-------------------------------------------------
--											                       --
--			BASES DE DATOS APLICADA		             --
--											                       --
-------------------------------------------------
-- GRUPO: 07                                   --
-- INTEGRANTES:								                 --
-- Mendoza, Diego Emanuel			                 --
-- Vazquez, Isaac Benjamin                     --
-- Pizarro Dorgan, Fabricio Alejandro          --
-- Piñero, Agustín                             --
-- Comerci Salcedo, Francisco Ivan             --
-------------------------------------------------
-------------------------------------------------
--											                       --
--		  STORED PROCEDURES REPORTES             --
--											                       --
-------------------------------------------------

USE Com5600G07
GO

-------------------------------------------------
--											                       --
--		  STORED PROCEDURES REPORTES             --
--											                       --
-------------------------------------------------

CREATE OR ALTER PROCEDURE report.sp_ReporteFlujoCajaSemanal
    @FechaInicio DATE,
    @FechaFin DATE,
    @IdConsorcio INT = NULL
AS
BEGIN
    SET NOCOUNT ON;

    -- 1. Obtenemos todos los pagos en el rango de fechas, uniéndolos con su
    --    prorrateo para saber qué porcentaje del PAGO corresponde a ordinario y extraord.
    WITH PagosConProrrateo AS (
        SELECT 
            p.Fecha,
            p.Importe,
            e.idConsorcio,
            -- ISNULL para evitar divisiones por cero si una expensa fue $0
            pr.ExpensaOrdinaria,
            pr.ExpensaExtraordinaria,
            ISNULL(pr.ExpensaOrdinaria, 0) + ISNULL(pr.ExpensaExtraordinaria, 0) AS TotalProrrateado
        FROM 
            Pago.Pago p
        INNER JOIN 
            expensas.Prorrateo pr ON p.NroExpensa = pr.NroExpensa AND p.IdUF = pr.IdUF
        INNER JOIN 
            expensas.Expensa e ON pr.NroExpensa = e.nroExpensa
        WHERE 
            p.Fecha BETWEEN @FechaInicio AND @FechaFin
            AND (@IdConsorcio IS NULL OR e.idConsorcio = @IdConsorcio)
    ),

    -- 2. Calculamos el importe exacto del pago para cada categoría (Ord/Extra)
    PagosCalculados AS (
        SELECT
            Fecha,
            idConsorcio,
            -- Si TotalProrrateado es 0, asignamos el 100% del pago a Ordinario
            CASE 
                WHEN TotalProrrateado = 0 THEN Importe
                ELSE Importe * (ISNULL(ExpensaOrdinaria, 0) / TotalProrrateado)
            END AS PagoOrdinario,
            CASE 
                WHEN TotalProrrateado = 0 THEN 0
                ELSE Importe * (ISNULL(ExpensaExtraordinaria, 0) / TotalProrrateado)
            END AS PagoExtraordinario
        FROM 
            PagosConProrrateo
    ),

    -- 3. Agrupamos esos pagos por AÑO y SEMANA
    DatosSemanales AS (
        SELECT
            YEAR(Fecha) AS Anio,
            DATEPART(week, Fecha) AS Semana,
            SUM(PagoOrdinario) AS RecaudacionOrdinaria,
            SUM(PagoExtraordinario) AS RecaudacionExtraordinaria,
            SUM(PagoOrdinario + PagoExtraordinario) AS TotalSemana
        FROM 
            PagosCalculados
        GROUP BY 
            YEAR(Fecha), DATEPART(week, Fecha)
    )

    -- 4. Presentamos el resultado final usando Funciones de Ventana para
    --    cumplir con los requisitos de "promedio" y "acumulado" 
    SELECT 
        Anio,
        Semana,
        RecaudacionOrdinaria,
        RecaudacionExtraordinaria,
        TotalSemana,
        
        -- Acumulado progresivo 
        SUM(TotalSemana) OVER (
            ORDER BY Anio, Semana 
            ROWS UNBOUNDED PRECEDING
        ) AS AcumuladoProgresivo,
        
        -- Promedio en el periodo 
        AVG(TotalSemana) OVER () AS PromedioPeriodo
    FROM 
        DatosSemanales
    ORDER BY 
        Anio, Semana;

END
GO

--PARA PROBARLO
EXEC report.sp_Reporte1_FlujoCajaSemanal 
    @FechaInicio = '2025-01-01', 
    @FechaFin = '2025-12-31', 
    @IdConsorcio = 1; -- Opcional para prueba
